apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

apply from: './scripts/artifact_naming.gradle'
apply from: './scripts/secret.gradle'

android {
    compileSdkVersion 29

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'sample.kotlin.project'

        minSdkVersion 21
        targetSdkVersion 29

        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME

        multiDexEnabled true
        multiDexKeepProguard file('multidex-config.pro')
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments clearPackageData: 'true'

        useLibrary 'android.test.runner'
        useLibrary 'android.test.base'
        useLibrary 'android.test.mock'

        buildConfigField 'String', 'LOG_TAG', '"SKP"'

        resConfigs 'en'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    signingConfigs {
        debug {
            storeFile file('debug.jks')
            storePassword 'htQVSM3d'
            keyAlias 'debug-key'
            keyPassword '8mrmvU6x'
            // SHA1: E1:AC:CA:EE:01:59:E7:BB:BF:AB:09:B7:E1:B7:B1:0D:AB:3B:43:27
            // SHA256: 7E:1D:E1:B8:10:AF:03:76:60:FA:39:A3:69:6E:01:A3:97:6E:1B:DF:85:54:7F:DF:70:02:96:6F:B7:D4:EF:08
        }
        release {
            storeFile getSecretKeystoreFile()
            storePassword getSecretKeystorePassword()
            keyAlias getSecretKeyAlias()
            keyPassword getSecretKeyPassword()
            // SHA1: FB:2E:01:1A:93:16:DA:6C:85:32:B6:57:37:C3:7D:D6:B0:24:17:16
            // SHA256: 0B:33:DE:0C:45:6A:AB:B0:E3:F4:30:37:6A:75:DA:40:DE:B6:57:86:67:B8:61:6B:96:12:8A:30:CF:DA:99:D4
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '-DEBUG'
            applicationIdSuffix '.debug'
            resValue 'string', 'app_name', "${APPLICATION_NAME}${versionNameSuffix}"
            resValue 'string', 'file_provider_authorities', "${defaultConfig.applicationId}${applicationIdSuffix}"
            debuggable true
            signingConfig signingConfigs.debug

            shrinkResources false
            minifyEnabled false
            proguardFiles 'proguard.cfg', 'proguard-rules.pro'
            buildConfigField 'boolean', 'ENABLE_MINIFY', "${minifyEnabled}"

            manifestPlaceholders += [screenOrientation: 'unspecified']

            manifestPlaceholders += [crashlyticsCollectionEnabled: 'false']
            ext.enableCrashlytics = false
            buildConfigField 'boolean', 'ENABLE_CRS', 'false'

            // logging
            buildConfigField 'boolean', 'PRINT_LOGS_TO_CRS', 'true'
            buildConfigField 'boolean', 'PRINT_LOGS_TO_FILE', 'true'
            buildConfigField 'boolean', 'PRINT_LOGS_TO_LOGCAT', 'true'
            buildConfigField 'boolean', 'PRINT_SENSITIVE_DATA', 'true'
            buildConfigField 'boolean', 'DUMP_OUT_OF_MEMORY', 'false'

            // network
            buildConfigField 'boolean', 'PRINT_HTTP_LOGS', 'true'
            buildConfigField 'boolean', 'PRETTY_JSON', 'true'
            buildConfigField 'boolean', 'USE_CERTIFICATE_PINNING', 'false'
            buildConfigField 'boolean', 'USE_FAKE_INTERCEPTOR', 'true'
        }
        qa {
            versionNameSuffix '-QA'
            applicationIdSuffix '.qa'
            resValue 'string', 'app_name', "${APPLICATION_NAME}${versionNameSuffix}"
            resValue 'string', 'file_provider_authorities', "${defaultConfig.applicationId}${applicationIdSuffix}"
            debuggable true
            signingConfig signingConfigs.debug
            matchingFallbacks = ['release', 'debug']

            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard.cfg', 'proguard-rules.pro'
            buildConfigField 'boolean', 'ENABLE_MINIFY', "${minifyEnabled}"

            manifestPlaceholders += [screenOrientation: 'portrait']

            manifestPlaceholders += [crashlyticsCollectionEnabled: 'true']
            ext.enableCrashlytics = true
            buildConfigField 'boolean', 'ENABLE_CRS', 'true'

            // logging
            buildConfigField 'boolean', 'PRINT_LOGS_TO_CRS', 'true'
            buildConfigField 'boolean', 'PRINT_LOGS_TO_FILE', 'true'
            buildConfigField 'boolean', 'PRINT_LOGS_TO_LOGCAT', 'true'
            buildConfigField 'boolean', 'PRINT_SENSITIVE_DATA', 'true'
            buildConfigField 'boolean', 'DUMP_OUT_OF_MEMORY', 'false'

            // network
            buildConfigField 'boolean', 'PRINT_HTTP_LOGS', 'true'
            buildConfigField 'boolean', 'PRETTY_JSON', 'true'
            buildConfigField 'boolean', 'USE_CERTIFICATE_PINNING', 'false'
            buildConfigField 'boolean', 'USE_FAKE_INTERCEPTOR', 'false'
        }
        release {
            versionNameSuffix ''
            applicationIdSuffix ''
            resValue 'string', 'app_name', "${APPLICATION_NAME}${versionNameSuffix}"
            resValue 'string', 'file_provider_authorities', "${defaultConfig.applicationId}${applicationIdSuffix}"
            //debuggable true
            signingConfig signingConfigs.release

            shrinkResources true
            minifyEnabled true
            proguardFiles 'proguard.cfg', 'proguard-rules.pro'
            buildConfigField 'boolean', 'ENABLE_MINIFY', "${minifyEnabled}"

            manifestPlaceholders += [screenOrientation: 'portrait']

            manifestPlaceholders += [crashlyticsCollectionEnabled: 'true']
            ext.enableCrashlytics = true
            buildConfigField 'boolean', 'ENABLE_CRS', 'true'

            // logging
            buildConfigField 'boolean', 'PRINT_LOGS_TO_CRS', 'true'
            buildConfigField 'boolean', 'PRINT_LOGS_TO_FILE', 'false'
            buildConfigField 'boolean', 'PRINT_LOGS_TO_LOGCAT', 'false'
            buildConfigField 'boolean', 'PRINT_SENSITIVE_DATA', 'false'
            buildConfigField 'boolean', 'DUMP_OUT_OF_MEMORY', 'false'

            // network
            buildConfigField 'boolean', 'PRINT_HTTP_LOGS', 'false'
            buildConfigField 'boolean', 'PRETTY_JSON', 'false'
            buildConfigField 'boolean', 'USE_CERTIFICATE_PINNING', 'true'
            buildConfigField 'boolean', 'USE_FAKE_INTERCEPTOR', 'false'
        }

        flavorDimensions 'server'
        productFlavors {
            dev {
                buildConfigField 'String', 'SERVER', '"https://server-dev"'
            }
            prod {
                buildConfigField 'String', 'SERVER', '"https://server-prod"'
            }
        }

        lintOptions {
            abortOnError false
        }

        sourceSets {
            // Example: how to add resources directories
            // main.res.srcDirs += 'src/main/res-flags'
            String sharedTestDir = 'src/sharedTest/java'
            test {
                java.srcDir sharedTestDir
            }
            androidTest {
                java.srcDir sharedTestDir
            }
        }
    }

    dependencies {
        // Testing
        testImplementation 'junit:junit:4.13'
        androidTestImplementation 'androidx.test:rules:1.2.0'
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

        androidTestUtil 'androidx.test:orchestrator:1.2.0'

        // Leaks tool
        debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'

        // TTLE tool
        implementation 'com.gu.android:toolargetool:0.2.1@aar'

        // Kotlin
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        // Android X
        implementation 'androidx.appcompat:appcompat:1.1.0'
        implementation 'androidx.core:core-ktx:1.2.0'
        implementation 'androidx.recyclerview:recyclerview:1.1.0'
        implementation 'androidx.cardview:cardview:1.0.0'
        implementation 'androidx.gridlayout:gridlayout:1.0.0'
        implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
        implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
        implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
        implementation 'com.google.android.material:material:1.1.0'

        // Rx
        implementation 'com.jakewharton.rxbinding3:rxbinding:3.1.0'
        implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
        implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
        implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
        implementation 'com.github.ReactiveX:RxKotlin:2.4.0'

        // Firebase: crashes, analytics
        implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
        implementation 'com.google.firebase:firebase-analytics:17.2.2'

        // DI
        api 'com.google.dagger:dagger:2.26'
        kapt 'com.google.dagger:dagger-compiler:2.26'
        api 'com.google.dagger:dagger-android:2.26'
        api 'com.google.dagger:dagger-android-support:2.26'
        kapt 'com.google.dagger:dagger-android-processor:2.26'

        // Logging
        implementation 'io.logging:logging-library:1.0.1'
        implementation 'org.slf4j:slf4j-api:1.7.30'
        implementation 'com.github.tony19:logback-android:2.0.0'

        // HTTP
        implementation 'com.squareup.retrofit2:retrofit:2.7.1'
        implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.1'
        implementation 'com.squareup.retrofit2:converter-gson:2.7.1'
        implementation 'com.squareup.okhttp3:logging-interceptor:4.4.0'

        // JSON
        implementation 'com.google.code.gson:gson:2.8.6'

        // Navigation
        implementation 'ru.terrakok.cicerone:cicerone:5.1.0'

        // Shared Preferences
        implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

        // Time
        implementation 'net.danlew:android.joda:2.10.3'
        testImplementation 'joda-time:joda-time:2.10.5'

        // Pictures
        implementation 'com.github.bumptech.glide:glide:4.11.0'
        kapt 'com.github.bumptech.glide:compiler:4.11.0'

        // Lists
        implementation 'com.hannesdorfmann:adapterdelegates4:4.2.0'
    }
}
